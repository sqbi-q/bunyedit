plugins {
	id 'fabric-loom' version '1.7.bta'
	id 'java'
}

group = project.mod_group
archivesBaseName = project.mod_name
version = project.mod_version

// Implement LWJGL for development runtime, so it can find liblwjgl

import org.gradle.internal.os.OperatingSystem
project.ext.lwjglVersion = "3.3.3"

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		break
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = "natives-windows"
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = "natives-macos"
}

//


loom {
	noIntermediateMappings()
	customMinecraftManifest.set("https://downloads.betterthanadventure.net/bta-client/${project.bta_channel}/v${project.bta_version}/manifest.json")
}

repositories {
	mavenCentral()
	maven { url = "https://jitpack.io" }
	maven {
		name = 'Babric'
		url = 'https://maven.glass-launcher.net/babric'
	}
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
	maven {
		name = 'SignalumMavenInfrastructure'
		url = 'https://maven.thesignalumproject.net/infrastructure'
	}
	maven {
		name = 'SignalumMavenReleases'
		url = 'https://maven.thesignalumproject.net/releases'
	}
	ivy {
		url = "https://github.com/Better-than-Adventure"
		patternLayout {
			artifact "[organisation]/releases/download/v[revision]/[module].jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}

	ivy {
		url = "https://downloads.betterthanadventure.net/bta-client/${project.bta_channel}/"
		patternLayout {
			artifact "/v[revision]/client.jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}
	ivy {
		url = "https://downloads.betterthanadventure.net/bta-server/${project.bta_channel}/"
		patternLayout {
			artifact "/v[revision]/server.jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}

	ivy {
		url = "https://piston-data.mojang.com"
		patternLayout {
			artifact "v1/[organisation]/[revision]/[module].jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}
}

configurations {
	embed
}

dependencies {
	minecraft "::${project.bta_version}"
	mappings loom.layered() {}

	modRuntimeOnly "objects:client:43db9b498cb67058d2e12d394e6507722e71bb45" // https://piston-data.mojang.com/v1/objects/43db9b498cb67058d2e12d394e6507722e71bb45/client.jar
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Helper library
	// If you do not need Halplibe you can comment this line out or delete this line
	modImplementation "turniplabs:halplibe:${project.halplibe_version}"

	// include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:0.2.0-beta.9")))

	modImplementation "turniplabs:modmenu-bta:${project.modmenu_version}"

	implementation "org.slf4j:slf4j-api:1.8.0-beta4"
	implementation "org.apache.logging.log4j:log4j-slf4j18-impl:2.16.0"

	implementation "org.jparsec:jparsec:3.1"
	embed "org.jparsec:jparsec:3.1"

	implementation 'com.google.guava:guava:30.0-jre'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
	var log4jVersion = "2.20.0"
	implementation("org.apache.logging.log4j:log4j-core:${log4jVersion}")
	implementation("org.apache.logging.log4j:log4j-api:${log4jVersion}")
	implementation("org.apache.logging.log4j:log4j-1.2-api:${log4jVersion}")

	implementation "org.lwjgl:lwjgl:$lwjglVersion"
	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	withSourcesJar()
}

tasks.withType(JavaCompile) {
	options.release.set 8
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}" }
	}
	from { configurations.embed.collect { it.isDirectory() ? it : zipTree(it) } }
}

processResources {
	inputs.property "version", version

	filesMatching("fabric.mod.json") {
		expand "version": version
	}
}
